#include "../GlobalDefines.cginc"
#ifndef DX11
    #pragma use_dxc
#endif
#include "CommonData.cginc"
#include "Materials.cginc"

#ifdef HDRP
    Texture2DArray<float2> MotionVectors;
#else
    Texture2D<float2> MotionVectors;
#endif

inline Ray CreateCameraRayGI(float2 uv, uint pixel_index, float4x4 CamToWorldMat, float4x4 CamInvProjMat) {
    float3 origin = mul(CamToWorldMat, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;

    float3 direction = mul(CamInvProjMat, float4(uv, 0.0f, 1.0f)).xyz;

    direction = normalize(mul(CamToWorldMat, float4(direction, 0.0f)).xyz);
    return CreateRay(origin, direction);
}


#ifdef HardwareRT
    #include "UnityRayQuery.cginc"
    #pragma require inlineraytracing
    RaytracingAccelerationStructure myAccelerationStructure;
#endif
bool VisibilityCheck(Ray ray, float dist) {
    #ifdef HardwareRT
        const uint rayFlags = RAY_FLAG_FORCE_OPAQUE  ;
        UnityRayQuery<rayFlags> q;
        RayDesc myRay;
        myRay.Origin = ray.origin;
        myRay.Direction = ray.direction;
        myRay.TMin = 0;
        myRay.TMax = dist;
        q.TraceRayInline(myAccelerationStructure, rayFlags, 0xff, myRay);
        while(q.Proceed()){}

        if(q.CommittedStatus() == COMMITTED_TRIANGLE_HIT) return false;
        else return true;
    #else
    return VisibilityCheckCompute(ray, dist);
    #endif
}

#pragma kernel ReSTIRGIKernel

float4x4 PrevCamToWorld;
float4x4 PrevCamInvProj;

float3 CamDelta;
bool AbandonSamples;
bool UseReSTIRGITemporal;
bool UseReSTIRGISpatial;
bool DoIndirectClamping;
bool DoReSTIRGIConnectionValidation;
int ReSTIRGISpatialCount;
int ReSTIRGITemporalMCap;
float GISpatialRadius;

inline void UpdateReservoir(inout float4 CurRes, inout float wsum, float4 PrevRes, float rndnum, inout int2 CachedID, int2 ID) {
    float w = max(PrevRes.x * PrevRes.y * PrevRes.w, 0);
    wsum += w;
    if (rndnum < (w / wsum)) {
        CachedID = ID;
        CurRes.w = PrevRes.w;
        CurRes.z = PrevRes.z;
    }
}




[numthreads(12, 12, 1)]
void ReSTIRGIKernel(uint3 id : SV_DispatchThreadID, uint3 id2 : SV_GroupThreadID, uint3 id3 : SV_GroupID, uint id4 : SV_GroupIndex) {

    if(id.x > screen_width || id.y > screen_height) return;
    const int pixel_index = id.x + id.y * screen_width;

            float2 UV = id.xy / float2(screen_width, screen_height);
    const Ray CameraRay = CreateCameraRayGI(UV * 2.0f - 1.0f, pixel_index, CamToWorld, CamInvProj);

    bool ActuallyRender = (TempAlbedoTex[id.xy].w > 0) && !AbandonSamples;
    
    const float4 GBuffer = ScreenSpaceInfo[id.xy];
    const float3 GeomNorm = i_octahedral_32(asuint(GBuffer.x));
    const float3 SurfNorm = i_octahedral_32(asuint(GBuffer.y));
    const float3x3 NormTangentSpace = GetTangentSpace2(GeomNorm);
    const float3x3 NormTangentSpaceSurf = GetTangentSpace2(SurfNorm);
    float3 PrimaryHitPosition = CameraRay.origin + CameraRay.direction * GBuffer.z;
    ColData CenterCol = GlobalColors[pixel_index];

    CenterCol.throughput.x = pixel_index;
    CenterCol.throughput.y = frames_accumulated;
    uint4 WorldPos = WorldPosC[id.xy];
    float4 NEEPos = NEEPosC[id.xy];

    MaterialData SurfaceMat = _Materials[GBuffer.w];
    float3 Surf = unpackRGBE(WorldPos.w);
    SurfaceMat.surfaceColor = unpackRGBE(WorldPos.w);
    SurfaceMat.roughness = (asuint(CenterCol.pad) & 0xFFFF) / 65535.0f;
    SurfaceMat.metallic = (asuint(CenterCol.pad) >> 16) / 65535.0f;


    float3 bsdf, NEEbsdf;
    float pdf, NEEpdf;
    CenterCol.Indirect = min(CenterCol.Indirect, 250.0f);//I NEED to find a better way of handling fireflies
    CenterCol.PrimaryNEERay *= EvaluateBsdf(SurfaceMat, CameraRay.direction, normalize(NEEPos.xyz - PrimaryHitPosition), GeomNorm, NEEpdf, NEEbsdf, pixel_index);
    bool Valid2 = ReconstructBsdf(SurfaceMat, CameraRay.direction, normalize(asfloat(WorldPos.xyz) - PrimaryHitPosition), GeomNorm, pdf, bsdf, NormTangentSpace, pixel_index);
    float Lum = luminance(Valid2 * (CenterCol.Direct + CenterCol.Indirect) * bsdf) + luminance(unpackRGBE(CenterCol.PrimaryNEERay) * NEEbsdf);
    float4 CurrentRes = {0,0,CenterCol.IsSpecular,clamp(Lum, 0, 3000)};

    #ifdef HDRP
        float2 MotionVector = MotionVectors[int3(UV * float2(TargetWidth, TargetHeight),0)].xy;
    #else
        float2 MotionVector = MotionVectors.SampleLevel(my_linear_clamp_sampler, UV, 0).xy;
    #endif

    uint2 prevIndex = int2(floor(((float2(id.xy) + 0.5) * float2(rcp(screen_width), rcp(screen_height)) - MotionVector) * float2(screen_width, screen_height)));

    int2 CachedID = -1;

    if(ActuallyRender) {
        GlobalColors[pixel_index].Direct = 0;
        GlobalColors[pixel_index].Indirect = 0;
        GlobalColors[pixel_index].PrimaryNEERay = 0;
        bool SkipTemporal = false;
        if(ReSTIRGIUpdateRate != 0) {
            if(RandomNums[id.xy].z == 1) {
                if(abs(NEEPosB[prevIndex].w - NEEPos.w) > 0.1f * max(max(NEEPosB[prevIndex].w, NEEPos.w),0.1f)) {
                    CenterCol.PrimaryNEERay = 0;
                    CenterCol.Direct = 0;
                    CenterCol.Indirect = 0;
                    NEEPos.w = 0;
                    CurrentRes.x = 1;
                    CurrentRes.y = 1;
                    CenterCol.throughput.x = 0;
                } else {
                    CenterCol.throughput = PrevGlobalColorsA[prevIndex.x + prevIndex.y * screen_width].throughput;
                    CurrentRes.xy = ReservoirB[prevIndex].xy;
                }
                SkipTemporal = true;
            }
        }
        float wsum = CurrentRes.w;

        bool SuccessfullyReprojected = SkipTemporal;
        if(!SkipTemporal) {
            if(UseReSTIRGITemporal) {

                float depth_prev = PrevScreenSpaceInfo[prevIndex].z;
                float3  geo_normal_prev = i_octahedral_32(asuint(PrevScreenSpaceInfo[prevIndex].x));//needs to be TEX_PT_GEO_NORMAL_B, but since for now I am not worrying about normal maps yet, it can use the same texture
                
                float dist_depth = (abs(GBuffer.z - depth_prev) - length(CamDelta)) / abs(GBuffer.z);
                float dot_geo_normals = dot(GeomNorm, geo_normal_prev);

                if (dist_depth < 0.1f && dot_geo_normals > 0.7)
                {
                    float4 PrevRes = ReservoirB[prevIndex];

                    PrevRes.x = clamp(PrevRes.x,1,ReSTIRGITemporalMCap);
                    UpdateReservoir(CurrentRes, wsum, PrevRes, randomNEE(23, pixel_index).x, CachedID, prevIndex);
                    CurrentRes.x += PrevRes.x;
                    SuccessfullyReprojected = true;
                    
                }
            }

            CurrentRes.x++;
            CurrentRes.y = wsum / max(CurrentRes.x * CurrentRes.w, 0.0000001f);
        }
        if(!SuccessfullyReprojected) {
            CurrentRes.x = 1;
            CurrentRes.y = 1;
        }

        if(CurrentRes.y > 10) CurrentRes.y = 0;
        if(CachedID.x != -1) {
            ColData TempCol = PrevGlobalColorsA[CachedID.x + CachedID.y * screen_width];
            uint4 TempWorldPos = WorldPosB[CachedID];
            float4 TempNEEPos = NEEPosB[CachedID];
            TempCol.PrimaryNEERay *= EvaluateBsdf(SurfaceMat, CameraRay.direction, normalize(TempNEEPos.xyz - PrimaryHitPosition), GeomNorm, NEEpdf, NEEbsdf, pixel_index);
            bool Valid4 = ReconstructBsdf(SurfaceMat, CameraRay.direction, normalize(asfloat(TempWorldPos.xyz) - PrimaryHitPosition), GeomNorm, pdf, bsdf, NormTangentSpace, pixel_index);
            if(Valid4) {
                CenterCol = TempCol;
                float Lum = luminance((CenterCol.Direct + CenterCol.Indirect) * bsdf) + luminance(unpackRGBE(CenterCol.PrimaryNEERay) * NEEbsdf);
                CurrentRes.zw = float2(TempCol.IsSpecular, clamp(Lum,0,3000));
                WorldPos = TempWorldPos.xyzw;
                NEEPos = TempNEEPos;
            }
        }

        PrevGlobalColorsB[pixel_index] = CenterCol;
        ReservoirA[id.xy] = CurrentRes;
        WorldPosA[id.xy] = WorldPos;
        NEEPosA[id.xy] = NEEPos;

         if(ReSTIRGIUpdateRate != 0) {
            if(RandomNums[id.xy].z == 1) {
                    CenterCol.PrimaryNEERay = 0;
                    CenterCol.Direct = 0;
                    CenterCol.Indirect = 0;
                    NEEPos.w = 0;
                    CurrentRes.x = 1;
                    CurrentRes.y = 1;
                    CenterCol.throughput.x = 0;
                }
            }

        if (UseReSTIRGISpatial) {
            uint2 CenterIndex = prevIndex;
            bool IsMirror = SurfaceMat.metallic == 1 && SurfaceMat.roughness < 0.01f;
            for (int i = 0; i < ReSTIRGISpatialCount; i++) {
                float2 RandOffsets = randomNEE(i, pixel_index);
                float2 RandOffsets2 = randomNEE(i + 23, pixel_index);

                float angle = 2.0f * PI * RandOffsets.x;
                int2 neighborOffset = vogelDiskSample(i, ReSTIRGISpatialCount, RandOffsets.y, angle) * RandOffsets2.x * GISpatialRadius * (IsMirror ? 0.25f : 1.0f);
                int2 NeighborIndex = CenterIndex + neighborOffset;

                if (NeighborIndex.x < 0 || NeighborIndex.x >= (int)screen_width || NeighborIndex.y < 0 || NeighborIndex.y >= (int)screen_height) continue;

                float4 PrevRes = ReservoirB[NeighborIndex];
                float4 PrevGBuffer = PrevScreenSpaceInfo[NeighborIndex];

                float DistDiff = (abs(GBuffer.z - PrevGBuffer.z)) / (24.0f * length(CamDelta) + 1);
                if (DistDiff >= 0.1f || dot(i_octahedral_32(asuint(PrevGBuffer.x)), GeomNorm) < 0.9 || CurrentRes.z != PrevRes.z) continue;
                
                UpdateReservoir(CurrentRes, wsum, PrevRes, randomNEE(23, pixel_index).x, CachedID, NeighborIndex);
                CurrentRes.x += PrevRes.x;
            }

            CurrentRes.y = wsum / max(CurrentRes.x * CurrentRes.w, 0.0000001f);
        }

        if (CurrentRes.y > 10) CurrentRes.y = 0;
         SurfaceMat.surfaceColor = Surf;//unpackRGBE(WorldPos.w);

        if(CachedID.x != -1) {
            ColData TempCol = PrevGlobalColorsA[CachedID.x + CachedID.y * screen_width];
            uint4 TempWorldPos = WorldPosB[CachedID];
            float4 TempNEEPos = NEEPosB[CachedID];
            TempCol.PrimaryNEERay *= EvaluateBsdf(SurfaceMat, CameraRay.direction, normalize(TempNEEPos.xyz - PrimaryHitPosition), SurfNorm, NEEpdf, NEEbsdf, pixel_index);
            bool Valid4 = ReconstructBsdf(SurfaceMat, CameraRay.direction, normalize(asfloat(TempWorldPos.xyz) - PrimaryHitPosition), SurfNorm, pdf, bsdf, NormTangentSpaceSurf, pixel_index);
            if(Valid4) {
                CenterCol = TempCol;
                float Lum = luminance((CenterCol.Direct + CenterCol.Indirect) * bsdf) + luminance(unpackRGBE(CenterCol.PrimaryNEERay) * NEEbsdf);
                CurrentRes.zw = float2(TempCol.IsSpecular, Lum);
                WorldPos = TempWorldPos;
                NEEPos = TempNEEPos;
            } else {
                return;
            }
        } else {
            CenterCol.PrimaryNEERay *= EvaluateBsdf(SurfaceMat, CameraRay.direction, normalize(NEEPos.xyz - PrimaryHitPosition), SurfNorm, NEEpdf, NEEbsdf, pixel_index);
            bool Valid4 = ReconstructBsdf(SurfaceMat, CameraRay.direction, normalize(asfloat(WorldPos.xyz) - PrimaryHitPosition), SurfNorm, pdf, bsdf, NormTangentSpaceSurf, pixel_index);
            if(!Valid4) return;
        }

        if (DoReSTIRGIConnectionValidation) {
            PrimaryHitPosition += GeomNorm * 0.01f;
            float Distance = length(PrimaryHitPosition - asfloat(WorldPos.xyz)) - 0.025f;
            Ray ray = CreateRay(PrimaryHitPosition, normalize(asfloat(WorldPos.xyz) - PrimaryHitPosition));

            bool InLineOfSight = VisibilityCheck(ray, Distance);
            if (!InLineOfSight) {CenterCol.Direct = 0; CenterCol.Indirect = 0; }
            #ifdef ExtraSampleValidation
                if (CenterCol.PrimaryNEERay != 0) {
                    Distance = length(PrimaryHitPosition - NEEPos.xyz) - 0.025f;
                    ray = CreateRay(PrimaryHitPosition, normalize(NEEPos.xyz - PrimaryHitPosition));

                    InLineOfSight = VisibilityCheck(ray, Distance);
                    if (!InLineOfSight) {
                        CenterCol = PrevGlobalColorsB[pixel_index];
                        CurrentRes = ReservoirA[id.xy];
                        WorldPos = WorldPosA[id.xy];
                        NEEPos = NEEPosA[id.xy];
                    }
                }
            #endif
        }

        if (!(CenterCol.IsSpecular == 3)) TempAlbedoTex[id.xy] = float4(abs(bsdf), 1);

        GlobalColors[pixel_index].Direct = CenterCol.Direct * CurrentRes.y;
        GlobalColors[pixel_index].Indirect = CenterCol.Indirect * CurrentRes.y;
        GlobalColors[pixel_index].PrimaryNEERay = packRGBE(unpackRGBE(CenterCol.PrimaryNEERay) * CurrentRes.y * NEEbsdf);
    } else {
        PrevGlobalColorsB[pixel_index] = CenterCol;
        ReservoirA[id.xy] = CurrentRes;
        WorldPosA[id.xy] = WorldPos;
        NEEPosA[id.xy] = NEEPos;
    }
}

